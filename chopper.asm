
; SPDX-FileName: chopper.asm
; SPDX-FileCopyrightText: Fort Apocalypse Â© 1995, 2007, 2015 Steve Hales.
; SPDX-FileContributor: Modified by Scott Giese 2023
; SPDX-License-Identifier: CC-BY-NC-ND-2.5


;=======================================
; at exit:
;   C           completion status
;=======================================
CheckChrI       .proc
;v_???          .var ADR1_I
;v_???          .var ADR2_I
;---

                jsr ComputeMapAddrI

                ldy #0
                lda (ADR1_I),y
                and #$7F
                ldy #0
                sty ADR2_I+1
                asl
                rol ADR2_I+1
                asl
                rol ADR2_I+1
                asl
                rol ADR2_I+1

                clc
                adc #<CHR_SET2
                sta ADR2_I
                lda #>CHR_SET2
                adc ADR2_I+1
                sta ADR2_I+1

                ldy #7
_next1          lda (ADR2_I),y
                bne _XIT

                dey
                bpl _next1

                clc
                rts

_XIT            sec
                rts
                .endproc


;=======================================
;
;=======================================
DoChopper       .proc
;v_???          .var ADR1_I
;v_???          .var TEMP1_I
;v_???          .var TEMP2_I
;v_???          .var TEMP3_I
;v_???          .var TEMP4_I
;---

                lda CHOPPER_STATUS
                cmp #kOFF
                bne _1

_XIT            rts

_leap           jmp _8

_1              cmp #kCRASH
                beq _XIT

                cmp #kLAND
                beq _2

                cmp #kPICKUP
                beq _2

                lda FRAME
                and GRAV_SKL
                bne _2

                inc CHOPPER_Y
_2              lda CHOPPER_COL
                beq _3

                cmp #4
                beq _leap               ; LASER

                cmp #8
                bne _4                  ; HYPER

                lda LEVEL
                bne _leap

                sta CHOPPER_COL
                lda #HYPERSPACE_MODE
                sta MODE

                lda #1
                sta SND3_VAL
                sta SND5_VAL
_3              ldx #kFLY
                bra _9

_4              lda CHOP_X
                sta TEMP1_I
                lda CHOP_Y
                sta TEMP2_I
                jsr ComputeMapAddrI

                lda #0
                sta TEMP3_I
                sta TEMP4_I
                jsr CheckLanding

                inc ADR1_I+1
                jsr CheckLanding

                inc ADR1_I+1
                jsr CheckLanding

                lda CHOPPER_COL
                and #%11110000
                bne _8

                lda CHOPPER_COL
                and #%00000010
                beq _5

                jsr SlavePickUp
                bcc _5

                ldx #kPICKUP
                jmp _9

_5              lda TEMP3_I
                cmp #3
                beq _8

                dec CHOPPER_Y
                ldx FUEL_STATUS
                lda CHOP_Y
                cmp #10+4
                blt _6

                cpx #kEMPTY
                beq _8

_6              cpx #kREFUEL
                beq _7

                lda TEMP4_I
                bne _7

                jsr RestorePoint

_7              ldx #kLAND
                bra _9

_8              lda #20
                sta TIM3_VAL
                lda #1
                sta SND3_VAL
                ldx #kCRASH
_9              stx CHOPPER_STATUS
                rts
                .endproc


;=======================================
;
;=======================================
UpdateChopper   .proc
;v_???          .var ADR1_I
;v_???          .var TEMP1_I
;v_???          .var TEMP2_I
;---

                lda CHOPPER_STATUS
                cmp #kBEGIN
                beq _1

                cmp #kOFF
                bne _2

                lda #0
                .sta_spr_xpos 0
                .sta_spr_xpos 1
                rts

_1              lda #kFLY
                sta CHOPPER_STATUS
                jmp _CCXY

_2              ldy OCHOPPER_Y
                ldx #17
                lda #0
_next1          sta PLAYER+PL0,y
                sta PLAYER+PL1,y
                iny
                dex
                bpl _next1

                lda CHOPPER_X
                .sta_spr_xpos 0
                clc
                adc #8
                .sta_spr_xpos 1

                lda CHOPPER_ANGLE
                asl
                tax
                lda CHOPPER_SHAPES,x
                sta ADR1_I
                lda CHOPPER_SHAPES+1,x
                sta ADR1_I+1

                lda #0
                sta TEMP1_I
                lda #18
                sta TEMP2_I

                ldx CHOPPER_Y
                stx OCHOPPER_Y

_next2          ldy TEMP1_I
                lda (ADR1_I),y
                sta PLAYER+PL0,x
                ldy TEMP2_I
                lda (ADR1_I),y
                sta PLAYER+PL1,x

                inc TEMP1_I
                inc TEMP2_I
                inx

                lda TEMP1_I
                cmp #18
                bne _next2

                lda CHOPPER_STATUS
                cmp #kCRASH
                bne _5

                ldx CHOPPER_Y
                ldy #18
_next3          lda PLAYER+PL0,x
                and SID_RANDOM
                sta PLAYER+PL0,x
                lda PLAYER+PL1,x
                and SID_RANDOM
                sta PLAYER+PL1,x
                inx
                dey
                bne _next3

                ;!! inc PCOLR0
                ;!! inc PCOLR1
                lda SID_RANDOM
                ora #$F
                sta BAK2_COLOR
                lda MODE
                cmp #GO_MODE
                bne _5

                lda FRAME
                and #1
                bne _3

                inc CHOPPER_Y
_3              dec TIM3_VAL
                bne _5

                lda R_STATUS
                cmp #kOFF
                beq _4

                jsr PositionRobot

                lda #kOFF
                sta R_STATUS
_4              jsr PositionChopper

                lda #NEW_PLAYER_MODE
                sta MODE

_5              lda FRAME
                and #3
                bne _6

                lda CHOPPER_ANGLE
                eor #1
                sta CHOPPER_ANGLE

_6              lda MODE
                cmp #GO_MODE
                bne _XIT

                jsr PositionChopper

_CCXY           lda CHOPPER_X
                sec
                sbc #24
                lsr
                lsr
                clc
                adc SX
                sta CHOP_X
                lda CHOPPER_Y
                sec
                sbc #76+12
                lsr
                lsr
                lsr
                sta TEMP1_I
                lda SY
                bpl _7

                lda #0
_7              clc
                adc TEMP1_I
                sta CHOP_Y
                jmp PositionChopper

_XIT            rts
                .endproc


;=======================================
;
;=======================================
PositionChopper .proc
v_posX          .var TEMP1_I
v_posY          .var TEMP2_I
;---

                lda CHOP_X
                sta v_posX
                lda CHOP_Y
                sta v_posY
                jmp PositionRobot.POS_IT_I

                .endproc


;=======================================
;
;=======================================
Hover           .proc
                lda FRAME
                and #7
                bne _XIT

                lda CHOPPER_ANGLE
                cmp #4
                blt _1

                cmp #14
                blt _XIT

_1              cmp #8
                bge _2

                inc CHOPPER_ANGLE
                inc CHOPPER_ANGLE
                rts

_2              dec CHOPPER_ANGLE
                dec CHOPPER_ANGLE
_XIT            rts
                .endproc

;---------------------------------------
;---------------------------------------

CHOPPER_SHAPES  .addr CL3_1,CL3_2       ; 0   ANGLE
                .addr CL2_1,CL2_2       ; 2
                .addr CL1_1,CL1_2       ; 4

                .addr CM1_1,CM1_2       ; 6
                .addr CM1_1,CM1_2       ; 8
                .addr CM1_1,CM1_2       ; 10

                .addr CR1_1,CR1_2       ; 12
                .addr CR2_1,CR2_2       ; 14
                .addr CR3_1,CR3_2       ; 16

;---------------------------------------
; Chopper Left-facing
;---------------------------------------
CL1_1           .byte $00,$00,$FF,$01,$01,$0F,$11,$21,$31                   ; ........ ........
                .byte $7F,$70,$3F,$1F,$10,$A0,$7F,$00,$00                   ; ........ ........
                                                                            ; ######## ##......
                .byte $00,$00,$C0,$00,$02,$02,$82,$FE,$0F                   ; .......# ........
                .byte $79,$61,$C1,$C0,$40,$20,$FC           ;,$00,$00       ; .......# ......#.
                                                                            ; ....#### ......#.
                                                                            ; ...#...# #.....#.
                                                                            ; ..#....# #######.
                                                                            ; ..##...# ....####
                                                                            ; .####### .####..#
                                                                            ; .###.... .##....#
                                                                            ; ..###### ##.....#
                                                                            ; ...##### ##......
                                                                            ; ...#.... .#......
                                                                            ; #.#..... ..#.....
                                                                            ; .####### ######..
                                                                            ; ........
                                                                            ; ........

CL1_2           .byte $00,$00,$07,$01,$01,$0F,$11,$21,$31                   ; ........ ........
                .byte $7F,$70,$3F,$1F,$10,$A0,$7F,$00,$00                   ; ........ ........
                                                                            ; .....### #######.
                .byte $00,$00,$FE,$00,$01,$01,$81,$FF,$0E                   ; .......# ........
                .byte $7A,$62,$C2,$C0,$40,$20,$FC           ;,$00,$00       ; .......# .......#
                                                                            ; ....#### .......#
                                                                            ; ...#...# #......#
                                                                            ; ..#....# ########
                                                                            ; ..##...# ....###.
                                                                            ; .####### .####.#.
                                                                            ; .###.... .##...#.
                                                                            ; ..###### ##....#.
                                                                            ; ...##### ##......
                                                                            ; ...#.... .#......
                                                                            ; #.#..... ..#.....
                                                                            ; .####### ######..
                                                                            ; ........
                                                                            ; ........

CL2_1           .byte $00,$00,$07,$F9,$01,$07,$09,$11,$21                   ; ........ ........
                .byte $37,$7C,$73,$3F,$18,$10,$A7,$78,$00                   ; ........ ........
                                                                            ; .....### ##......
                .byte $00,$00,$C0,$02,$02,$02,$9E,$FF,$19                   ; #####..# ......#.
                .byte $71,$61,$C0,$C0,$60,$3C,$C0           ;,$00,$00       ; .......# ......#.
                                                                            ; .....### ......#.
                                                                            ; ....#..# #..####.
                                                                            ; ...#...# ########
                                                                            ; ..#....# ...##..#
                                                                            ; ..##.### .###...#
                                                                            ; .#####.. .##....#
                                                                            ; .###..## ##......
                                                                            ; ..###### ##......
                                                                            ; ...##... .##.....
                                                                            ; ...#.... ..####..
                                                                            ; #.#..### ##......
                                                                            ; .####...
                                                                            ; ........

CL2_2           .byte $00,$00,$07,$01,$01,$07,$09,$11,$21                   ; ........ ........
                .byte $37,$7C,$73,$3F,$18,$10,$A7,$78,$00                   ; ........ ..#####.
                                                                            ; .....### ##......
                .byte $00,$3E,$C0,$01,$01,$01,$9F,$FE,$1A                   ; .......# .......#
                .byte $72,$62,$C0,$C0,$60,$3C,$C0           ;,$00,$00       ; .......# .......#
                                                                            ; .....### .......#
                                                                            ; ....#..# #..#####
                                                                            ; ...#...# #######.
                                                                            ; ..#....# ...##.#.
                                                                            ; ..##.### .###..#.
                                                                            ; .#####.. .##...#.
                                                                            ; .###..## ##......
                                                                            ; ..###### ##......
                                                                            ; ...##... .##.....
                                                                            ; ...#.... ..####..
                                                                            ; #.#..### ##......
                                                                            ; .####...
                                                                            ; ........

CL3_1           .byte $00,$00,$03,$3D,$C1,$03,$0D,$11,$21                   ; ........ ........
                .byte $23,$6E,$79,$67,$3E,$10,$11,$9E,$60                   ; ........ ........
                                                                            ; ......## #.......
                .byte $00,$00,$80,$02,$02,$02,$8E,$FF,$39                   ; ..####.# ......#.
                .byte $61,$61,$C0,$C0,$60,$3C,$E0           ;,$00,$00       ; ##.....# ......#.
                                                                            ; ......## ......#.
                                                                            ; ....##.# #...###.
                                                                            ; ...#...# ########
                                                                            ; ..#....# ..###..#
                                                                            ; ..#...## .##....#
                                                                            ; .##.###. .##....#
                                                                            ; .####..# ##......
                                                                            ; .##..### ##......
                                                                            ; ..#####. .##.....
                                                                            ; ...#.... ..####..
                                                                            ; ...#...# ###.....
                                                                            ; #..####.
                                                                            ; .##.....

CL3_2           .byte $00,$00,$03,$01,$01,$03,$0D,$11,$21                   ; ........ .....##.
                .byte $23,$6E,$79,$67,$3E,$10,$11,$9E,$60                   ; ........ .####...
                                                                            ; ......## #.......
                .byte $06,$78,$80,$01,$01,$01,$8F,$FE,$3A                   ; .......# .......#
                .byte $62,$62,$C0,$C0,$60,$3C,$E0           ;,$00,$00       ; .......# .......#
                                                                            ; ......## .......#
                                                                            ; ....##.# #...####
                                                                            ; ...#...# #######.
                                                                            ; ..#....# ..###.#.
                                                                            ; ..#...## .##...#.
                                                                            ; .##.###. .##...#.
                                                                            ; .####..# ##......
                                                                            ; .##..### ##......
                                                                            ; ..#####. .##.....
                                                                            ; ...#.... ..####..
                                                                            ; ...#...# ###.....
                                                                            ; #..####.
                                                                            ; .##.....

;---------------------------------------
; Chopper Middle-facing
;---------------------------------------
CM1_1           .byte $00,$00,$07,$00,$00,$01,$03,$06,$04                   ; ........ ........
                .byte $08,$0D,$07,$03,$04,$08,$1C,$00,$00                   ; ........ ........
                                                                            ; .....### ########
                .byte $00,$00,$FF,$80,$80,$C0,$E0,$30,$10                   ; ........ #.......
                .byte $08,$D8,$F0,$E0,$10,$08,$1C           ;,$00,$00       ; ........ #.......
                                                                            ; .......# ##......
                                                                            ; ......## ###.....
                                                                            ; .....##. ..##....
                                                                            ; .....#.. ...#....
                                                                            ; ....#... ....#...
                                                                            ; ....##.# ##.##...
                                                                            ; .....### ####....
                                                                            ; ......## ###.....
                                                                            ; .....#.. ...#....
                                                                            ; ....#... ....#...
                                                                            ; ...###.. ...###..
                                                                            ; ........
                                                                            ; ........

CM1_2           .byte $00,$00,$7F,$00,$00,$01,$03,$06,$04                   ; ........ ........
                .byte $08,$0D,$07,$03,$04,$08,$1C,$00,$00                   ; ........ ........
                                                                            ; .####### ###.....
                .byte $00,$00,$E0,$80,$80,$C0,$E0,$30,$10                   ; ........ #.......
                .byte $08,$D8,$F0,$E0,$10,$08,$1C           ;,$00,$00       ; ........ #.......
                                                                            ; .......# ##......
                                                                            ; ......## ###.....
                                                                            ; .....##. ..##....
                                                                            ; .....#.. ...#....
                                                                            ; ....#... ....#...
                                                                            ; ....##.# ##.##...
                                                                            ; .....### ####....
                                                                            ; ......## ###.....
                                                                            ; .....#.. ...#....
                                                                            ; ....#... ....#...
                                                                            ; ...###.. ...###..
                                                                            ; ........
                                                                            ; ........

;---------------------------------------
; Chopper Right-facing
;---------------------------------------
CR1_1           .byte $00,$00,$03,$00,$40,$40,$41,$7F,$F0                   ; ........ ........
                .byte $9E,$86,$83,$03,$02,$04,$3F,$00,$00                   ; ........ ........
                                                                            ; ......## ########
                .byte $00,$00,$FF,$80,$80,$F0,$88,$84,$8C                   ; ........ #.......
                .byte $FE,$0E,$FC,$F8,$08,$05,$FE           ;,$00,$00       ; .#...... #.......
                                                                            ; .#...... ####....
                                                                            ; .#.....# #...#...
                                                                            ; .####### #....#..
                                                                            ; ####.... #...##..
                                                                            ; #..####. #######.
                                                                            ; #....##. ....###.
                                                                            ; #.....## ######..
                                                                            ; ......## #####...
                                                                            ; ......#. ....#...
                                                                            ; .....#.. .....#.#
                                                                            ; ..###### #######.
                                                                            ; ........
                                                                            ; ........

CR1_2
                .byte $00,$00,$7F,$00,$80,$80,$81,$FF,$70                   ; ........ ........
                .byte $5E,$46,$43,$03,$02,$04,$3F,$00,$00                   ; ........ ........
                                                                            ; .####### ###.....
                .byte $00,$00,$E0,$80,$80,$F0,$88,$84,$8C                   ; ........ #.......
                .byte $FE,$0E,$FC,$F8,$08,$05,$FE           ;,$00,$00       ; #....... #.......
                                                                            ; #....... ####....
                                                                            ; #......# #...#...
                                                                            ; ######## #....#..
                                                                            ; .###.... #...##..
                                                                            ; .#.####. #######.
                                                                            ; .#...##. ....###.
                                                                            ; .#....## ######..
                                                                            ; ......## #####...
                                                                            ; ......#. ....#...
                                                                            ; .....#.. .....#.#
                                                                            ; ..###### #######.
                                                                            ; ........
                                                                            ; ........

CR2_1           .byte $00,$00,$03,$40,$40,$40,$79,$FF,$98                   ; ........ ........
                .byte $8E,$86,$03,$03,$06,$3C,$03,$00,$00                   ; ........ ........
                                                                            ; ......## ###.....
                .byte $00,$00,$E0,$9F,$80,$E0,$90,$88,$84                   ; .#...... #..#####
                .byte $EC,$3E,$CE,$FC,$18,$08,$E5,$1E           ;,$00       ; .#...... #.......
                                                                            ; .#...... ###.....
                                                                            ; .####..# #..#....
                                                                            ; ######## #...#...
                                                                            ; #..##... #....#..
                                                                            ; #...###. ###.##..
                                                                            ; #....##. ..#####.
                                                                            ; ......## ##..###.
                                                                            ; ......## ######..
                                                                            ; .....##. ...##...
                                                                            ; ..####.. ....#...
                                                                            ; ......## ###..#.#
                                                                            ; ........ ...####.
                                                                            ; ........

CR2_2           .byte $00,$7C,$03,$80,$80,$80,$F9,$7F,$58                   ; ........ ........
                .byte $4E,$46,$03,$03,$06,$3C,$03,$00,$00                   ; .#####.. ........
                                                                            ; ......## ###.....
                .byte $00,$00,$E0,$80,$80,$E0,$90,$88,$84                   ; #....... #.......
                .byte $EC,$3E,$CE,$FC,$18,$08,$E5,$1E           ;,$00       ; #....... #.......
                                                                            ; #....... ###.....
                                                                            ; #####..# #..#....
                                                                            ; .####### #...#...
                                                                            ; .#.##... #....#..
                                                                            ; .#..###. ###.##..
                                                                            ; .#...##. ..#####.
                                                                            ; ......## ##..###.
                                                                            ; ......## ######..
                                                                            ; .....##. ...##...
                                                                            ; ..####.. ....#...
                                                                            ; ......## ###..#.#
                                                                            ; ........ ...####.
                                                                            ; ........

CR3_1           .byte $00,$00,$01,$40,$40,$40,$71,$FF,$9C                   ; ........ ........
                .byte $86,$86,$03,$03,$06,$3C,$07,$00,$00                   ; ........ ........
                                                                            ; .......# ##......
                .byte $00,$00,$C0,$BC,$83,$C0,$B0,$88,$84                   ; .#...... #.####..
                .byte $C4,$76,$9E,$E6,$7C,$08,$88,$79,$06                   ; .#...... #.....##
                                                                            ; .#...... ##......
                                                                            ; .###...# #.##....
                                                                            ; ######## #...#...
                                                                            ; #..###.. #....#..
                                                                            ; #....##. ##...#..
                                                                            ; #....##. .###.##.
                                                                            ; ......## #..####.
                                                                            ; ......## ###..##.
                                                                            ; .....##. .#####..
                                                                            ; ..####.. ....#...
                                                                            ; .....### #...#...
                                                                            ; ........ .####..#
                                                                            ; ........ .....##.

CR3_2           .byte $60,$1E,$01,$80,$80,$80,$F1,$7F,$5C                   ; .##..... ........
                .byte $46,$46,$03,$03,$06,$3C,$07,$00,$00                   ; ...####. ........
                                                                            ; .......# ##......
                .byte $00,$00,$C0,$80,$80,$C0,$B0,$88                       ; #....... #.......
BOOT_STUFF      .byte $84,$C4,$76,$9E,$E6,$7C,$08,$88,$79,$06               ; #....... #.......
                                                                            ; #....... ##......
                                                                            ; ####...# #.##....
                                                                            ; .####### #...#...
                                                                            ; .#.###.. #....#..
                                                                            ; .#...##. ##...#..
                                                                            ; .#...##. .###.##.
                                                                            ; ......## #..####.
                                                                            ; ......## ###..##.
                                                                            ; .....##. .#####..
                                                                            ; ..####.. ....#...
                                                                            ; .....### #...#...
                                                                            ; ........ .####..#
                                                                            ; ........ .....##.

;---------------------------------------

                .byte $66,$65,$70,$6C,$4C,$44,$50,$4C,$61,$3B,$DC,$95,$3A,$A8

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; EOF
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
